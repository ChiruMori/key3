import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "org.springframework.boot" version "2.3.4.RELEASE"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "checkstyle"
}

group "work.cxlm"
version "3.1.0"
description "system for club management"

repositories {
    mavenLocal()
    mavenCentral()
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('application.yml') {
        filter ReplaceTokens, tokens: [app_version: project.property('version')]
    }
}

configurations {
    implementation {
        exclude module: 'spring-boot-starter-tomcat'
        exclude module: 'junit'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    hutoolVersion = "5.3.8"
    commonsLangVersion = "3.10"
    levelDbVersion = "0.12"
    jedisVersion = "2.9.3"
    swaggerVersion = "2.9.2"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-jetty"
    implementation "org.springframework.boot:spring-boot-starter-freemarker"
    // 封装了表单验证相关注解
    implementation "org.springframework.boot:spring-boot-starter-validation"
    // Emoji 处理工具类
    implementation group: 'com.github.binarywang', name: 'java-emoji-converter', version: '1.0.1'
    // Http 请求工具
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    // 热加载
    implementation group: 'org.springframework.boot', name: 'spring-boot-devtools', version: '2.3.4.RELEASE'
    // Bean Utils
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    // Redisson
    compile group: 'org.redisson', name: 'redisson-spring-data-21', version: '3.11.5'
    // autoconfigure
    implementation 'com.github.drtrang:spring-boot2-autoconfigure:1.1.8'
    // caffeine
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.0'
    implementation 'com.github.ben-manes.caffeine:guava:2.8.0'

    implementation "com.sun.mail:jakarta.mail"
    implementation "cn.hutool:hutool-core:$hutoolVersion"
    implementation "cn.hutool:hutool-crypto:$hutoolVersion"
    implementation "cn.hutool:hutool-extra:$hutoolVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation "io.springfox:springfox-swagger2:$swaggerVersion"
    implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"

    // 缓存
    // LevelDB是 Google 开源的持久化 KV 单机数据库，具有很高的随机写、顺序读写性能，解决了 Redis 双写导致的一系列问题
    implementation "org.iq80.leveldb:leveldb:$levelDbVersion"
    implementation "redis.clients:jedis:$jedisVersion"
    runtimeOnly "mysql:mysql-connector-java"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}
